/*******************************************************************************
 * Copyright (c) 2017 Association Cénotélie (cenotelie.fr)
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Lesser General Public License as
 * published by the Free Software Foundation, either version 3
 * of the License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General
 * Public License along with this program.
 * If not, see <http://www.gnu.org/licenses/>.
 ******************************************************************************/

grammar Fixture : HimeGrammar
{
	options
	{
		Axiom = "fixture";
		Separator = "SEPARATOR";
	}
	terminals
	{
		context tree
		{
			NODE_NAME -> NAME | 'ε'; // for the head of sub rules
		}
	}
	rules
	{
		fixture			-> header^ test* ;
		header			-> 'fixture'! NAME^ ;
		test			-> test_matches^
						|  test_no_match^
						|  test_fails^
						|  test_output^ ;

		test_matches	-> 'test'! NAME ':'! cf_grammar 'parser'! NAME 'on'! LITERAL_STRING 'yields'! tree ;
		test_no_match	-> 'test'! NAME ':'! cf_grammar 'parser'! NAME 'on'! LITERAL_STRING 'differs'! tree ;
		test_fails		-> 'test'! NAME ':'! cf_grammar 'parser'! NAME 'on'! LITERAL_STRING 'fails'! ;
		test_output		-> 'test'! NAME ':'! cf_grammar 'parser'! NAME 'on'! LITERAL_STRING 'outputs'! LITERAL_STRING* ;

		tree		-> #tree { NODE_NAME^ } check children ;
		check		-> ;
		check		-> '=' LITERAL_TEXT ;
		check		-> '!=' LITERAL_TEXT ;
		children	-> ;
		children	-> '('! tree* ')'! ;
	}
}

grammar ExpectedTree : Fixture
{
	options
	{
		Axiom = "tree";
		Separator = "SEPARATOR";
	}
	terminals { }
	rules { }
}